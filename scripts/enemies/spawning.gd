class_name Spawning

class SpawnEntry:
	var name: String
	var weight: float

	func _init(entry_name: String, entry_weight: float) -> void:
		self.name = entry_name
		self.weight = entry_weight

static var weed_enemies: Dictionary = {
	"weed" : preload("uid://bhnk8apyedtit"),
	"mini_thornweed" : preload("uid://bpn14mbnmv14h"),
	"shrubweed" : preload("uid://brq3scjfltm0n"),
	"thornweed": preload("uid://fqhlxrgabgqv"),
	"mushroom" : preload("uid://bfrhyuagid5eh"),
	"fungal_mother" : preload("uid://b60yupijenmi2"),
	"super_shroom" : preload("uid://deieyv2snbfnr"),
	"super_thornweed" : preload("uid://comqqrvdmt27s"),
}

static var flower_enemies: Dictionary = {
	"yellow" : preload("uid://kai60sy8qsix"),
	"red" : preload("uid://cuwajf6p2vxkf"),
	"blue" : preload("uid://1fh3tiwr4hty"),
}

static var mobile_enemies: Dictionary = {
	"shrub_demon" : preload("uid://d1jwu43vb0643"),
	"fungal_baby" : preload("uid://b1shkd3nhlmls"),
	"killer_rabbit" : preload("uid://b32ibsy1iagox"),
	"wasp" : preload("uid://c0pplynlnjxjg"),
	"wasp_queen" : preload("uid://b8lrxt2odr5gb"),
}

static var bosses : Dictionary = {
	"wasp_queen" : true
}

static var weed_spawn_table: Dictionary = {
	"easy" : [ 
		SpawnEntry.new("weed", 3.0), 
		SpawnEntry.new("mini_thornweed", 2.0),
	],

	"easy+" : [ 
		SpawnEntry.new("weed", 1.0), 
		SpawnEntry.new("mini_thornweed", 1.0),
	],

	"medium" : [ 
		SpawnEntry.new("weed", 3.0),
		SpawnEntry.new("mini_thornweed", 3.0), 
		SpawnEntry.new("shrubweed", 2.0),
		SpawnEntry.new("thornweed", 2.0),
	],

	"medium+" : [
		SpawnEntry.new("weed", 3.0),
		SpawnEntry.new("mini_thornweed", 3.0), 
		SpawnEntry.new("shrubweed", 2.0),
		SpawnEntry.new("thornweed", 2.0),
		SpawnEntry.new("mushroom", 2.0), 
	],

	"medium++" : [
		SpawnEntry.new("weed", 1.0),
		SpawnEntry.new("mini_thornweed", 1.0), 
		SpawnEntry.new("shrubweed", 1.0),
		SpawnEntry.new("thornweed", 1.0),
		SpawnEntry.new("mushroom", 1.0), 
	],

	"hard" : [
		SpawnEntry.new("weed", 2.0),
		SpawnEntry.new("mini_thornweed", 1.0), 
		SpawnEntry.new("shrubweed", 1.0),
		SpawnEntry.new("thornweed", 3.0),
		SpawnEntry.new("mushroom", 3.0),
		SpawnEntry.new("fungal_mother", 1.0),
	],

	"hard+" : [
		SpawnEntry.new("weed", 1.0),
		SpawnEntry.new("mini_thornweed", 2.0), 
		SpawnEntry.new("shrubweed", 1.0),
		SpawnEntry.new("thornweed", 6.0),
		SpawnEntry.new("mushroom", 5.0),
		SpawnEntry.new("fungal_mother", 2.0),
	],

	"hard++" : [
		SpawnEntry.new("weed", 1.0),
		SpawnEntry.new("mini_thornweed", 2.0),
		SpawnEntry.new("shrubweed", 1.0),
		SpawnEntry.new("thornweed", 8.0),
		SpawnEntry.new("mushroom", 7.0),
		SpawnEntry.new("fungal_mother", 4.0),
		SpawnEntry.new("super_shroom", 1.0),
		SpawnEntry.new("super_thornweed", 1.0),
	],

	"hard+++" : [
		SpawnEntry.new("mini_thornweed", 1.0),
		SpawnEntry.new("shrubweed", 1.0),
		SpawnEntry.new("thornweed", 7.0),
		SpawnEntry.new("mushroom", 7.0),
		SpawnEntry.new("fungal_mother", 5.0),
		SpawnEntry.new("super_shroom", 1.0),
		SpawnEntry.new("super_thornweed", 1.0),
	],
}

static var weed_count_table: Dictionary = {
	"easy" : [ 3, 1 ],
	"easy+" : [ 3, 3, 1 ],
	"medium" : [ 3, 3, 1 ],
	"medium+" : [ 3 ],
	"medium++" : [ 4, 3, 3 ],
	"hard" : [ 5, 4, 4, 3, 3, 3 ],
	"hard+" : [ 5, 4, 4, 3, 3, 3 ],
	"hard++" : [ 5, 4, 4, 3, 3 ],
	"hard+++" : [ 6, 5, 4, 4, 3 ],
}

static var flower_spawn_table: Dictionary = {
	"easy" : [
		SpawnEntry.new("yellow", 1.0), 
	],
	
	"easy+" : [
		SpawnEntry.new("yellow", 1.0), 
	],

	"medium" : [
		SpawnEntry.new("yellow", 2.0), 
		SpawnEntry.new("red", 1.0)
	],

	"medium+" : [
		SpawnEntry.new("yellow", 2.0), 
		SpawnEntry.new("red", 1.0)
	],

	"medium++" : [
		SpawnEntry.new("yellow", 3.0), 
		SpawnEntry.new("red", 2.0)
	],

	"hard" : [
		SpawnEntry.new("yellow", 1.0), 
		SpawnEntry.new("red", 1.0)
	],

	"hard+" : [
		SpawnEntry.new("yellow", 2.0), 
		SpawnEntry.new("red", 3.0),
		SpawnEntry.new("blue", 1.0)
	],

	"hard++" : [
		SpawnEntry.new("yellow", 2.0), 
		SpawnEntry.new("red", 3.0),
		SpawnEntry.new("blue", 2.0)
	],

	"hard+++" : [
		SpawnEntry.new("yellow", 1.0), 
		SpawnEntry.new("red", 2.0),
		SpawnEntry.new("blue", 3.0)
	]
}

static var mob_spawn_table: Dictionary = {
	"easy" : [],

	"easy+" : [ SpawnEntry.new("shrub_demon", 1.0) ],

	"medium" : [ SpawnEntry.new("shrub_demon", 1.0) ],

	"medium+" : [ SpawnEntry.new("shrub_demon", 1.0) ],

	"medium++" : [ 
		SpawnEntry.new("shrub_demon", 3.0), 
		SpawnEntry.new("fungal_baby", 1.0) 
	],

	"hard" : [ 
		SpawnEntry.new("shrub_demon", 2.0), 
		SpawnEntry.new("fungal_baby", 1.0),
		SpawnEntry.new("killer_rabbit", 1.0),
		SpawnEntry.new("random", 1.0),
	],
	
	"hard+" : [ 
		SpawnEntry.new("shrub_demon", 2.0), 
		SpawnEntry.new("fungal_baby", 2.0),
		SpawnEntry.new("killer_rabbit", 2.0),
		SpawnEntry.new("wasp", 1.0),
		SpawnEntry.new("random", 2.0),
	],

	"hard++" : [
		SpawnEntry.new("shrub_demon", 1.0), 
		SpawnEntry.new("fungal_baby", 2.0),
		SpawnEntry.new("killer_rabbit", 2.0),
		SpawnEntry.new("wasp", 2.0),
		SpawnEntry.new("random", 3.0),
	],

	"hard+++" : [
		SpawnEntry.new("shrub_demon", 2.0), 
		SpawnEntry.new("fungal_baby", 4.0),
		SpawnEntry.new("killer_rabbit", 4.0),
		SpawnEntry.new("wasp", 3.0),
		SpawnEntry.new("wasp_queen", 1.0),
		SpawnEntry.new("random", 3.0),
	],
}

static var mob_count_table: Dictionary = {
	"easy" : {},
	"easy+" : { 
		"shrub_demon" : [ 3, 2, 1 ]
	},
	"medium" : { 
		"shrub_demon" : [ 4, 3, 2 ] 
	},
	"medium+" : {
		"shrub_demon" : [ 4, 4, 3, 3, 2 ] 
	},
	"medium++" : { 
		"shrub_demon" : [ 4, 3, 3 ],
		"fungal_baby" : [ 3, 2, 2, 1 ]
	},
	"hard" : {
		"shrub_demon" : [ 6, 5, 4, 4, 4, 3, 3 ],
		"fungal_baby" : [ 4, 3, 3, 2, 2, 1 ],
		"killer_rabbit" : [ 3, 2, 2, 1 ],
		"random" : [ 4, 3, 3 ]
	},
	"hard+" : {
		"shrub_demon" : [ 6, 5, 4, 4 ],
		"fungal_baby" : [ 5, 4, 4, 3, 3, 2 ],
		"killer_rabbit" : [ 4, 3, 3, 2, 2 ],
		"wasp" : [ 4, 3, 2, 2 ],
		"random" : [ 7, 6, 5, 4, 4, 3, 3 ]
	},
	"hard++" : {
		"shrub_demon" : [ 7, 6, 5, 5, 4, 4 ],
		"fungal_baby" : [ 6, 5, 4, 4, 3 ],
		"killer_rabbit" : [ 4, 4, 3, 3, 2 ],
		"wasp" : [ 5, 4, 3, 3, 2 ],
		"random" : [ 7, 6, 5, 4, 4 ],
	},
	"hard+++" : {
		"shrub_demon" : [ 9, 8, 7, 6, 5, 5, 4, 4 ],
		"fungal_baby" : [ 6, 5, 5, 4, 4, 3 ],
		"killer_rabbit" : [ 4, 4, 3, 3, 2 ],
		"wasp" : [ 6, 5, 4, 4, 3, 3 ],
		"wasp_queen" : [ 1 ],
		"random" : [ 7, 6, 6, 5, 5, 4 ],
	},
}

static func int_difficulty_to_string(difficulty: int) -> String:
	match difficulty:
		0:
			return "easy"
		1:
			return "easy+"
		2:
			return "medium"
		3:
			return "medium+"
		4:
			return "medium++"
		5:
			return "hard"
		6:
			return "hard+"
		7:
			return "hard++"
		8:
			return "hard+++"
	return ""

# Returns an index based on the array of weights
# Example: [ 2.0, 2.0, 1.0 ]
# Indices 0 and 1 would have probability 2.0 / (2.0 + 2.0 + 1.0) = 0.4
# While index 2 would have half the probability at 1.0 / (2.0 + 2.0 + 1.0) = 0.2
static func get_rand(spawn_entries: Array) -> String:
	var total: float = 0.0
	for entry in spawn_entries:
		total += entry.weight
	if total == 0.0:
		return ""
	
	var val = randf()
	var current_total = 0.0
	for i in range(len(spawn_entries)):
		var weight = spawn_entries[i].weight / total
		if val >= current_total and val < current_total + weight:
			return spawn_entries[i].name
		current_total += weight
	
	return spawn_entries[max(len(spawn_entries) - 1, 0)].name

static func get_weed_spawn_weights(difficulty: int) -> Array:
	return weed_spawn_table[int_difficulty_to_string(difficulty)]

static func get_rand_weed_count(difficulty: int) -> int:
	var counts: Array = weed_count_table[int_difficulty_to_string(difficulty)]
	if counts.is_empty():
		return 0
	return counts[randi() % len(counts)]

static func instantiate_weed(id: String) -> WeedEnemy:
	return weed_enemies[id].instantiate()

static func get_weed_scene(id: String) -> PackedScene:
	return weed_enemies[id]

static func get_flower_spawn_weights(difficulty: int) -> Array:
	return flower_spawn_table[int_difficulty_to_string(difficulty)]

static func instantiate_flower(id: String) -> FlowerEnemy:
	return flower_enemies[id].instantiate()

# Returns the mob spawn weights with the entry "random" potentially included
# "random" means a collection of mobile enemies can be spawned, not just one type
static func get_mob_spawn_weights_random(difficulty: int) -> Array:
	return mob_spawn_table[int_difficulty_to_string(difficulty)]

static func get_mob_spawn_weights(difficulty: int) -> Array:
	var ret = []
	var weights = mob_spawn_table[int_difficulty_to_string(difficulty)]
	for entry in weights:
		if entry is SpawnEntry:
			if entry.name in bosses:
				continue
			if entry.name == "random":
				continue
		ret.push_back(entry)
	return ret

static func instantiate_mob(id: String) -> MobileEnemy:
	return mobile_enemies[id].instantiate()

static func get_mob_scene(id: String) -> PackedScene:
	return mobile_enemies[id]

static func get_rand_mob_count(difficulty: int, id: String) -> int:
	var count_table: Dictionary = mob_count_table[int_difficulty_to_string(difficulty)]
	if count_table.is_empty():
		return 0
	var counts: Array = count_table[id]
	if counts.is_empty():
		return 0
	return counts[randi() % len(counts)]

# Generates the enemy positions in a circle
# Parameters:
# The radius of the circle is calculated with the formula: radius = start + spacing * count
# Enemies are then generated at random positions in the distance between (radius - randomness) and radius
static func gen_enemy_positions_circle(
	count: int, 
	start: float, 
	spacing: float, 
	randomness: float
) -> Array:
	if count == 1:
		return [ Vector2(0.0, 0.0) ]

	var radius = start + float(count) * spacing
	var start_angle = randf() * PI * 2.0
	var positions: Array[Vector2] = []
	for i in range(count):
		var angle = start_angle + i * 2.0 * PI / count
		var dist = radius - randomness * randf()
		var x = cos(angle) * dist
		var y = sin(angle) * dist
		positions.append(Vector2(x, y))
	return positions

# distances are in tile units
# returns true if an enemy was successfull spawned, false otherwise
static func spawn_around_point(
	lawn: Lawn,
	parent: Node,
	position: Vector2,
	scene: PackedScene,
	min_dist: float,
	max_dist: float,
	rand_offset: float = 0.0,
	ignore_cut: bool = false,
) -> bool:
	if lawn == null:
		return false

	var node: Node2D = scene.instantiate()
	var dist: float = randf_range(min_dist, max_dist)
	var angle: float = randf_range(0.0, 2.0 * PI)
	var offset: Vector2 = Vector2(cos(angle) * lawn.tile_size.x, sin(angle) * lawn.tile_size.y) * dist
	var pos: Vector2 = position + offset
	var tile_pos: Vector2i = Vector2i(
		int(floor(pos.x / lawn.tile_size.x)),
		int(floor(pos.y / lawn.tile_size.y))
	)
	if !lawn.is_valid_spawn_tile(tile_pos.x, tile_pos.y):
		return false
	# Make sure all the surrounding tiles are grass
	for dx in range(-1, 2):
		for dy in range(-1, 2):
			if abs(dx) == 1 and abs(dy) == 1:
				continue
			var tile: Vector2i = lawn.get_tile(tile_pos.x + dx, tile_pos.y + dy)
			if !LawnGenerationUtilities.is_grass(tile) and !LawnGenerationUtilities.is_cut_grass(tile):
				return false
	var tile: Vector2i = lawn.get_tile(tile_pos.x, tile_pos.y)
	if LawnGenerationUtilities.is_cut_grass(tile) and ignore_cut:
		return false
	# Spawn the object centered on the tile
	node.global_position = Vector2(
		(tile_pos.x + 0.5) * lawn.tile_size.x, 
		(tile_pos.y + 0.5) * lawn.tile_size.y
	)
	# Add any offset
	var rand_offset_vec: Vector2 = Vector2(randf() - 0.5, randf() - 0.5) * rand_offset * 2.0
	rand_offset_vec.x *= lawn.tile_size.x
	rand_offset_vec.y *= lawn.tile_size.y
	node.global_position += rand_offset_vec
	parent.add_child(node)
	return true

# Spawns an item at a point without any checks
static func spawn_at_point(
	lawn: Lawn,
	parent: Node,
	position: Vector2,
	scene: PackedScene,
) -> Node2D:
	var node: Node2D = scene.instantiate()
	var tile_pos: Vector2i = Vector2i(
		int(floor(position.x / lawn.tile_size.x)),
		int(floor(position.y / lawn.tile_size.y))
	)
	# Spawn the object centered on the tile
	node.global_position = Vector2(
		(tile_pos.x + 0.5) * lawn.tile_size.x, 
		(tile_pos.y + 0.5) * lawn.tile_size.y
	)
	parent.add_child(node)
	return node

static func try_spawning_around_point(
	lawn: Lawn,
	parent: Node,
	position: Vector2,
	scene: PackedScene,
	min_dist: float,
	max_dist: float,
	tries: int,
	rand_offset: float = 0.0,
	ignore_cut: bool = false,
) -> bool:
	for i in range(tries):
		var res: bool = spawn_around_point(lawn, parent, position, scene, min_dist, max_dist, rand_offset, ignore_cut)
		if res:
			return true
	return false
